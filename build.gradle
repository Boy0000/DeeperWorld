buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

plugins {
    id 'java'
    id 'idea'
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.github.johnrengelman.shadow'

//Put your plugins folder directory here! If it is left as '', jars will only be copied if the parent project has a path specified
ext.plugin_path = ''

group = 'com.derongan.minecraft'
version = pluginVersion + "-SNAPSHOT"

sourceCompatibility = 1.8
targetCompatibility = 1.8
repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'rutger-repo'
        url = 'http://www.rutgerkok.nl/repo'
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    compileOnly 'org.spigotmc:spigot-api:1.15-R0.1-SNAPSHOT'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "nl.rutgerkok:blocklocker:1.7"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {
    getArchiveClassifier().set(null)
    minimize {
        exclude(dependency("org.jetbrains.kotlin:.*"))
    }
}

build.dependsOn shadowJar

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }

//Move into plugins folder
//TODO get the updated script in here
if (plugin_path != '') {
    println("Copying to plugin directory")
    task copyJar(type: Copy) {
        from shadowJar // here it automatically reads jar file produced from jar task
        into plugin_path
    }
    build.dependsOn copyJar
} else if (parent != null && parent.hasProperty("plugin_path")) {
    println("Copying to parent project's plugin directory")
    task copyJar(type: Copy) {
        from shadowJar
        into parent.ext.plugin_path
    }
    build.dependsOn copyJar
} else println("Skipped copyJar")